windowDef Desktop {
	rect		0 ,0 ,640 ,480
	backcolor		0.4 ,0.7 ,0.8 ,0.2
	float		"chamberstatus" -1
	
	onEvent {
		if ("chamberstatus" != "gui::gui_parm2") {
			
			// STAGE 1: Chamber is OPEN with NO ITEM ... Default
			if ("gui::gui_parm2" == 1) {
				set "chamberstatus" "1" ;
				resetTime "Chamberseq1" "0" ;
				set "networkState" "1" ;
			}
			
			// STAGE 2: Chamber is LOCKED and PROCESSING
			if ("gui::gui_parm2" == 2) {
				set "chamberstatus" "2" ;
				resetTime "Chamberseq2" "0" ;
				set "networkState" "2" ;
			}			
			
			// STAGE 3: Chamber is OPEN with the ITEM
			if ("gui::gui_parm2" == 3) {
				set "chamberstatus" "3" ;
				resetTime "Chamberseq3" "0" ;
				set "networkState" "3" ;
			}
		}		
	}
	
	onNamedEvent networkState {
		if ( "gui::networkState" == 1 ) {
			// STAGE 1: Chamber is OPEN with NO ITEM ... Default
			set		"Bar_TOP::visible" "0" ;
			set		"Bar_TOP_Text::visible" "0" ;
			set		"Bar_MIDDLE::visible" "0" ;
			set		"Bar_MIDDLE_Text::visible" "0" ;
			set		"Bar_BOTTOM::visible" "0" ;
			set		"Bar_BOTTOM_Text1::visible" "0" ;

			transition	"Bracket_TL::rect" "102 16 132 132" "132 46 132 132" "500" ;
			transition	"Bracket_TH::rect" "273 16 94 26" "273 46 94 26" "500" ;
			transition	"Bracket_TR::rect" "406 16 132 132" "376 46 132 132" "500" ;
			transition	"Bracket_V1::rect" "102 193 26 94" "132 193 26 94" "500" ;
			transition	"Bracket_V2::rect" "512 193 26 94" "482 193 26 94" "500" ;
			transition	"Bracket_BL::rect" "102 332 132 132" "132 302  132 132" "500" ;
			transition	"Bracket_BH::rect" "273 438 94 26" "273 408 94 26" "500" ;
			transition	"Bracket_BR::rect" "406 332 132 132" "376 302 132 132" "500" ;

			set		"Activate::matcolor" "0.5 0.8 0.8 0.3" ;
			set		"Activate::visible" "1" ;
		}
		if ( "gui::networkState" == 2 ) {
			// STAGE 2: Chamber is LOCKED and PROCESSING
			transition	"Activate::matcolor" "1 1 1 1" "1 1 1 0.3" "500" ;
			set 		"Activate::visible" "0" ;

			transition	"Bracket_TL::rect" "132 46 132 132" "102 16 132 132" "500" ;
			transition	"Bracket_TL::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_TH::rect" "273 46 94 26" "273 16 94 26" "500" ;
			transition	"Bracket_TH::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_TR::rect" "376 46 132 132" "406 16 132 132" "500" ;
			transition	"Bracket_TR::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_V1::rect" "132 193 26 94" "102 193 26 94" "500" ;
			transition	"Bracket_V1::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;

			transition	"Bracket_V2::rect" "482 193 26 94" "512 193 26 94" "500" ;
			transition	"Bracket_V2::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BL::rect" "132 302  132 132" "102 332 132 132" "500" ;
			transition	"Bracket_BL::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BH::rect" "273 408 94 26" "273 438 94 26" "500" ;
			transition	"Bracket_BH::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BR::rect" "376 302 132 132" "406 332 132 132" "500" ;
			transition	"Bracket_BR::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;

			set		"Bar_TOP::matcolor" "1 0 0 0.8" ;
			set		"Bar_TOP::visible" "1" ;
			set		"Bar_TOP_Text::text" "#str_00122" ;
			set		"Bar_TOP_Text::forecolor" "1 0.5 0.5 1" ;				
			set		"Bar_TOP_Text::visible" "1" ;
			set		"Bar_MIDDLE::matcolor" "0.4 0.7 0.8 0.8" ;
			set		"Bar_MIDDLE::visible" "1" ;
			set		"Bar_MIDDLE_Text::forecolor" "0.4 0.7 0.8 1" ;
			set		"Bar_MIDDLE_Text::visible" "1" ;
			set		"Bar_BOTTOM::matcolor" "1 0.8 0 0.3" ;
			set		"Bar_BOTTOM::visible" "1" ;
		}
		if ( "gui::networkState" == 3 ) {
			// STAGE 3: Chamber is OPEN with the ITEM
			set 		"Activate::visible" "0" ;
			transition	"Bar_TOP::matcolor" "1 0 0 0.8" "0 1 0 0.8" "200" ;
			set		"Bar_TOP_Text::text" "#str_01690" ;
			set		"Bar_TOP_Text::forecolor" "0.5 1 0.5 1" ;
			transition	"Bar_MIDDLE::matcolor" "0.4 0.7 0.8 0.8" "0.4 0.7 0.8 0.4" "200" ;
			set		"Bar_MIDDLE_Text::forecolor" "0.4 0.7 0.8 0.1" ;
			set		"Bar_MIDDLE_Text::visible" "0" ;

			transition	"Bar_BOTTOM::matcolor" "1 0.8 0 0.3" "1 0.8 0 0.8" "200" ;
			set		"Bar_BOTTOM_Text1::visible" "1" ;
			transition	"Bracket_TL::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_TH::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_TR::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_V1::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_V2::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_BL::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_BH::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_BR::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;			
		}
	}	

// -----------------------------------------------	
// Background image
// -----------------------------------------------

	windowDef BG
	{
		rect	0,0,640,480
		background	"guis/assets/alphalabs/fragbg"
		matcolor	0.4 ,0.7 ,0.8 ,0.25
		visible	1
	}
	windowDef BG1
	{
		rect	0,0,640,480
		background	"gui/alphalabs/fragbgmask"
		matcolor	0.9 ,0.8 ,0.5 ,0.5
		visible	1
	}
	windowDef BG2
	{
		rect	0,0,640,480
		background	"gui/alphalabs/fragbg"
		matcolor	0.3 ,0.25 ,0.1 ,0.3
		visible	1
	}
	windowDef BG11
	{
		rect	0,0,640,480
		background	"gui/alphalabs/fragbgmask2"
		matcolor	1,1,1,0.15
		visible	1

		onTime 0 {
			transition "matcolor" "1 1 1 0" "1 1 1 0.1" "1000" ;
		}

		onTime 6000 {
			transition "matcolor" "1 1 1 0.1" "1 1 1 0" "1000" ;	
		}

		onTime 7000 {
			resetTime "0" ;
		}

	}	
	
// -----------------------------------------------	
// Non-interactive feedback elements
// -----------------------------------------------	
	
	windowDef Bar_TOP {
		rect		178 ,90 ,284 ,87 
		background	"guis/assets/fragchamber/bar_top"
		matcolor	1 ,0 ,0 ,0.8
		visible		0
	}

	windowDef Bar_TOP_Text {
		rect		178 ,90 ,284 ,87 
		forecolor	1 ,0.5 ,0.5 ,1
		textScale	0.6
		textAlign	1
		textAligny	1
		text		"#str_00122"
		font		"fonts/micro"
		visible		0
	}
	
	windowDef Bar_MIDDLE {
		rect		178 ,193 ,284 ,94 
		background	"guis/assets/fragchamber/bar_middle"
		matcolor	0.4 ,0.7 ,0.8 ,0.8
		visible		0
	}

	windowDef Bar_MIDDLE_Text {
		rect		178 ,193 ,284 ,94  
		forecolor	0.4 ,0.7 ,0.8 ,1
		textScale	0.6
		textAlign	1
		textAligny	21
		text		"#str_00123"
		font		"fonts/micro"
		visible		0
	}
	
	windowDef Bar_BOTTOM {
		rect		178 ,302 ,284 ,87 
		background	"guis/assets/fragchamber/bar_bottom"
		matcolor	1 ,0.8 ,0 ,0.3
		visible		0
	}

	windowDef Bar_BOTTOM_Text1 {
		rect		178 ,302 ,284 ,87 
		forecolor	1 ,0.8 ,0 ,1
		textScale	0.6
		textAlign	1
		textAligny	20
		text		"#str_00475"
		font		"fonts/micro"
		visible		0
	}

// -----------------------------------------------	
// Main button to Activate the chamber
// -----------------------------------------------
	
	windowDef Activate {
		rect		168 ,88 ,304 ,304
		background	"guis/assets/fragchamber/activate"
		matcolor	0.5 ,0.8 ,0.8 ,0.3 
		
		textScale	0.8
		textAlign	1
		textAligny	100
		forecolor	1 ,1 ,1 ,1
		text		"#str_00128"
		font		"fonts/micro"
		visible		1
		
		onMouseEnter {
			transition	"matcolor" "0.5 0.8 0.8 0.3" "0.2 0.8 0.5 0.3" "200" ;
			transition	 "forecolor" "1 1 1 1" "0.6 1 0.6 1" "200" ; 
		} 
		
		onMouseExit {
			transition	"matcolor" "0.2 0.8 0.5 0.3" "0.5 0.8 0.8 0.3" "200" ; 
			transition 	"forecolor" "0.6 1 0.6 1" "1 1 1 1" "200" ; 
		} 		
		
		onAction {
			runScript 	"gui::gui_parm10" ;

			transition	"Activate::matcolor" "1 1 1 1" "1 1 1 0.3" "500" ;
			set 		"Activate::visible" "0" ;

			transition	"Bracket_TL::rect" "132 46 132 132" "102 16 132 132" "500" ;
			transition	"Bracket_TL::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_TH::rect" "273 46 94 26" "273 16 94 26" "500" ;
			transition	"Bracket_TH::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_TR::rect" "376 46 132 132" "406 16 132 132" "500" ;
			transition	"Bracket_TR::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_V1::rect" "132 193 26 94" "102 193 26 94" "500" ;
			transition	"Bracket_V1::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;

			transition	"Bracket_V2::rect" "482 193 26 94" "512 193 26 94" "500" ;
			transition	"Bracket_V2::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BL::rect" "132 302  132 132" "102 332 132 132" "500" ;
			transition	"Bracket_BL::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BH::rect" "273 408 94 26" "273 438 94 26" "500" ;
			transition	"Bracket_BH::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BR::rect" "376 302 132 132" "406 332 132 132" "500" ;
			transition	"Bracket_BR::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			
			set		"Bar_TOP::matcolor" "1 0 0 0.8" ;
			set		"Bar_TOP::visible" "1" ;
			set		"Bar_TOP_Text::text" "#str_00122" ;
			set		"Bar_TOP_Text::forecolor" "1 0.5 0.5 1" ;				
			set		"Bar_TOP_Text::visible" "1" ;
			set		"Bar_MIDDLE::matcolor" "0.4 0.7 0.8 0.8" ;
			set		"Bar_MIDDLE::visible" "1" ;
			set		"Bar_MIDDLE_Text::forecolor" "0.4 0.7 0.8 1" ;
			set		"Bar_MIDDLE_Text::visible" "1" ;
			set		"Bar_BOTTOM::matcolor" "1 0.8 0 0.3" ;
			set		"Bar_BOTTOM::visible" "1" ;
		}		
	}
	
// -----------------------------------------------	
// Window Brackets that move when the GUI is activated
// -----------------------------------------------
	
	windowDef Bracket_TL {
		rect		132 ,46 ,132 ,132 
		background	"guis/assets/fragchamber/bracket_tl"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}
	
	windowDef Bracket_TH {
		rect		273 ,46 ,94 ,26 
		background	"guis/assets/fragchamber/bracket_h"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}
	
	windowDef Bracket_TR {
		rect		376 ,46 ,132 ,132 
		background	"guis/assets/fragchamber/bracket_tr"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}
	
	windowDef Bracket_V1 {
		rect		132 ,193 ,26 ,94 
		background	"guis/assets/fragchamber/bracket_v"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}
	
	windowDef Bracket_V2 {
		rect		482 ,193 ,26 ,94 
		background	"guis/assets/fragchamber/bracket_v"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}
	
	windowDef Bracket_BL {
		rect		132 ,302  ,132 ,132 
		background	"guis/assets/fragchamber/bracket_bl"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}
	
	windowDef Bracket_BH {
		rect		273 ,408 ,94 ,26 
		background	"guis/assets/fragchamber/bracket_h"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}
	
	windowDef Bracket_BR {
		rect		376 ,302 ,132 ,132 
		background	"guis/assets/fragchamber/bracket_bR"
		matcolor	0.4 ,0.7 ,0.8 ,0.6
	}	

	// -----------------------------------------------	
	// Chamber sequence
	// -----------------------------------------------	
	// STAGE 1: Chamber is OPEN with NO ITEM ... Default
	windowDef Chamberseq1 {
		rect 0,0,0,0
		notime 1

		onTime 0 {
			set		"Bar_TOP::visible" "0" ;
			set		"Bar_TOP_Text::visible" "0" ;
			set		"Bar_MIDDLE::visible" "0" ;
			set		"Bar_MIDDLE_Text::visible" "0" ;
			set		"Bar_BOTTOM::visible" "0" ;
			set		"Bar_BOTTOM_Text1::visible" "0" ;
		}
		onTime 0 {
			transition	"Bracket_TL::rect" "102 16 132 132" "132 46 132 132" "500" ;
			transition	"Bracket_TH::rect" "273 16 94 26" "273 46 94 26" "500" ;
			transition	"Bracket_TR::rect" "406 16 132 132" "376 46 132 132" "500" ;
			transition	"Bracket_V1::rect" "102 193 26 94" "132 193 26 94" "500" ;
			transition	"Bracket_V2::rect" "512 193 26 94" "482 193 26 94" "500" ;
			transition	"Bracket_BL::rect" "102 332 132 132" "132 302  132 132" "500" ;
			transition	"Bracket_BH::rect" "273 438 94 26" "273 408 94 26" "500" ;
			transition	"Bracket_BR::rect" "406 332 132 132" "376 302 132 132" "500" ;
			
			
		}
		onTime 0 {
			set		"Activate::matcolor" "0.5 0.8 0.8 0.3" ;
			set		"Activate::visible" "1" ;				
		}
	}

	// STAGE 2: Chamber is LOCKED and PROCESSING
	windowDef Chamberseq2 {
		rect 0,0,0,0
		notime 1

		onTime 0 {
			transition	"Activate::matcolor" "1 1 1 1" "1 1 1 0.3" "500" ;
			set 		"Activate::visible" "0" ;
		}
		onTime 0 {
			transition	"Bracket_TL::rect" "132 46 132 132" "102 16 132 132" "500" ;
			transition	"Bracket_TL::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_TH::rect" "273 46 94 26" "273 16 94 26" "500" ;
			transition	"Bracket_TH::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_TR::rect" "376 46 132 132" "406 16 132 132" "500" ;
			transition	"Bracket_TR::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_V1::rect" "132 193 26 94" "102 193 26 94" "500" ;
			transition	"Bracket_V1::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
		}
		onTime 0 {
			transition	"Bracket_V2::rect" "482 193 26 94" "512 193 26 94" "500" ;
			transition	"Bracket_V2::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BL::rect" "132 302  132 132" "102 332 132 132" "500" ;
			transition	"Bracket_BL::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BH::rect" "273 408 94 26" "273 438 94 26" "500" ;
			transition	"Bracket_BH::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
			transition	"Bracket_BR::rect" "376 302 132 132" "406 332 132 132" "500" ;
			transition	"Bracket_BR::matcolor" "0.4 0.7 0.8 0.6" "0.4 0.7 0.8 0.9" "500" ;
		}
		onTime 0 {
			set		"Bar_TOP::matcolor" "1 0 0 0.8" ;
			set		"Bar_TOP::visible" "1" ;
			set		"Bar_TOP_Text::text" "#str_00122" ;
			set		"Bar_TOP_Text::forecolor" "1 0.5 0.5 1" ;				
			set		"Bar_TOP_Text::visible" "1" ;
			set		"Bar_MIDDLE::matcolor" "0.4 0.7 0.8 0.8" ;
			set		"Bar_MIDDLE::visible" "1" ;
			set		"Bar_MIDDLE_Text::forecolor" "0.4 0.7 0.8 1" ;
			set		"Bar_MIDDLE_Text::visible" "1" ;
			set		"Bar_BOTTOM::matcolor" "1 0.8 0 0.3" ;
			set		"Bar_BOTTOM::visible" "1" ;
		}
	}

	// STAGE 3: Chamber is OPEN with the ITEM
	windowDef Chamberseq3 {
		rect 0,0,0,0
		notime 1		
		
		// Redundancy from STAGE 2 to account for GUI catch-up ...
		onTime 0 {
			set 		"Activate::visible" "0" ;		
			set		"Bar_TOP::visible" "1" ;				
			set		"Bar_TOP_Text::visible" "1" ;
			set		"Bar_MIDDLE::visible" "1" ;
			set		"Bar_BOTTOM::visible" "1" ;
			set		"Bracket_TL::rect" "102 16 132 132" ;
			set		"Bracket_TH::rect" "273 16 94 26" ;
			set		"Bracket_TR::rect" "406 16 132 132" ;
			set		"Bracket_V1::rect" "102 193 26 94" ;
			set 		"Bracket_V2::rect" "512 193 26 94" ;
			set 		"Bracket_BL::rect" "102 332 132 132" ;
			set 		"Bracket_BH::rect" "273 438 94 26" ;
			set 		"Bracket_BR::rect" "406 332 132 132" ;
		}	// ... end Redundancy
		onTime 0 {
			set 		"Activate::visible" "0" ;
			transition	"Bar_TOP::matcolor" "1 0 0 0.8" "0 1 0 0.8" "200" ;
			set		"Bar_TOP_Text::text" "#str_01690" ;
			set		"Bar_TOP_Text::forecolor" "0.5 1 0.5 1" ;
			transition	"Bar_MIDDLE::matcolor" "0.4 0.7 0.8 0.8" "0.4 0.7 0.8 0.4" "200" ;
			set		"Bar_MIDDLE_Text::forecolor" "0.4 0.7 0.8 0.1" ;
			set		"Bar_MIDDLE_Text::visible" "0" ;
		}
		onTime 0 {
			transition	"Bar_BOTTOM::matcolor" "1 0.8 0 0.3" "1 0.8 0 0.8" "200" ;
			set		"Bar_BOTTOM_Text1::visible" "1" ;
			transition	"Bracket_TL::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_TH::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_TR::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_V1::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_V2::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_BL::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_BH::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;
			transition	"Bracket_BR::matcolor" "0.4 0.7 0.8 0.9" "0.4 0.7 0.8 0.6" "500" ;				
		}
	}

	windowDef OuterGlowBar
	{
		rect	-6,-6,-649,489
		background	"gui/outerglowbar2"
		matcolor	pdfade[ time * 0.001 ]/1.2 ,pdfade[ time * 0.001 ] ,pdfade[ time * 0.001 ] ,1
		visible	1
	}
	windowDef scanlinesAlt1
	{
		rect	0,0,-640,480
		matcolor	0.1,0.1,0.1,1
		background	"gui/test/gui_scanlines5"
		matscaley	2
		visible	1
	}
	windowDef Reflect
	{
		rect	34,1,-576,452
		matcolor	1 ,1 ,1 ,0.2
		visible	1
		background	"gui/reflect1"
		noevents	1
	}	
}