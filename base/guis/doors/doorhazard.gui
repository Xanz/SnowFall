windowDef Desktop {
	comment "setting gui_parm1 value 1 turns the gui green and changes toxic message."
	rect	0 ,0 ,640 ,480 
	background	"guis/assets/doors/hazard.tga"
	matcolor	0.8 ,0.1 ,0.1 ,pdfade [ time * 0.0005 ] / 1.2 
	backcolor	0 ,0 ,0 ,1
	float transition 0 

	onEvent {
		if ("gui::gui_parm1" == 1) {
			if ("transition" == 0) { 
				set "transition" "1" ;
				resetTime "Anim" "0" ; 
			}
		}
	} 

	windowDef Line3 {
		rect 		60, 95, 520, 230
		background	"guis/assets/doors/warnline2.tga"
		visible 	0
		matcolor 	0.2, 1, 0.5, 0.5

		onTime 0 {
			transition "matcolor" "0.2 1 0.5 0.2" "0.2 1 0.5 0" "1000" ;
		}
		onTime 1000 {
			transition "matcolor" "0.2 1 0.5 0" "0.2 1 0.5 0.2" "1000" ;
		}
		onTime 2000 {
			resetTime "0" ;
		}
	}

	windowDef Line2 {
		rect 60, 100, 517, 230
		background	"guis/assets/doors/warnline2.tga"
		visible 1
		matcolor 0.8 ,0.1 ,0.1, 0.5
	}

	windowDef Line1 {
		rect 60, 95, 520, 240
		background	"guis/assets/doors/warnline1.tga"
		visible 1
		matcolor 0.8 ,0.1 ,0.1, 0.5
	}

	windowDef WarningText {
		rect 112, 163, 430, 110
		text "#str_00044"
		textscale 1.8
		forecolor 1, 1, 1, 1

		onTime 0 {
			transition "forecolor" "1 0.2 0.1 1" "1 0.2 0.1 0" "800" ;
		//	set "Line1::matcolor" "0.8 0.1 0.1 0.5" ;
		//	set "Line2::matcolor" "0.8 0.1 0.1 0.5" ;
		}
		onTime 100 {
			transition "Line1::matcolor" "0.8 0.1 0.1 0.5" "0.8 0.1 0.1 0" "600" ;
		}
		onTime 200 {
			transition "Line2::matcolor" "0.8 0.1 0.1 0.5" "0.8 0.1 0.1 0" "600" ;
		}
		onTime 800 {
			resetTime "0" ;
		}
	}

	windowDef TextArea2 {
		rect		0 ,407 ,640 ,75 
		text "#str_01184"
		textscale 0.9
		textalign 1
		forecolor 1, 0.8, 0.4, 1
	}

	windowDef Safe {
		rect 100, 112, 440, 110
		text "#str_00925"
		textalign 1
		textscale 1.4
		forecolor 0.2, 1, 0.5, 0
	}

	windowDef Safe1 {
		rect 100, 178, 440, 140
		text "#str_01185"
		textscale 0.75
		textalign 1
		forecolor 1, 1, 1, 0
	}

	windowDef Safe2 {
		background	"guis/assets/doors/toxnom.tga"
		matcolor 	1, 0.8, 0.4, 0
		rect		75 ,376 ,500 ,75 
		visible		1
	}

	windowDef TextArea {
		rect 0, 358, 640, 60
		text "#str_01186"
		textscale 0.70
		font "fonts/bank"
		textalign 1
		forecolor 1, 1, 1, 0.8
	}

	windowDef Bglow {
		rect 0, 0, 640, 480
		background	"guis/assets/cpuserver/bglow.tga"
		visible 1
		matcolor 1, 0, 0, 0.1
	}


	windowDef Static {
	//static overlay
		rect	-10 ,-10 ,660 ,500 
		visible	0
		background "gui/static"
		matcolor	1, 1, 1, pdhalffade[ time * 0.001 ] / 6
	}

	windowDef Mask {
	//large black edge fade
		rect	0 ,0 ,640 ,480 
		visible	1
		background "guis/assets/test/mask"
		matcolor	1, 1, 1, 0.4
	}



	windowDef Outerglow {
	//pulsing outer edge glow
		rect	0 ,0 ,-640 ,480 
		background	"guis/assets/common/outerglow"
		matcolor	1 ,1 ,1 , pdflick [ time * .0025 ] / 2.8 //0.25
		visible	1
	}
	windowDef Outershadow {
	//small black edge fade
		rect	0 ,0 ,-640 ,480 
		background	"guis/assets/common/outershadow"
		matcolor	1 ,1 ,1 ,1 
		visible	1
	}
	windowDef Addhighlight {
	//light to trans overlay
		rect	0 ,0 ,-640 ,480 
		background	"gui/addhighlight"
		matcolor	0.3 ,0.8 ,0.8 ,1 
		visible	1
	}
	windowDef Scanlines {
	//scrolling raster lines
		rect	0,0,640,480
		matcolor	0.15,0.15,0.15,1
		background	"gui/test/gui_scanlines5"
		visible	1
		matscaley 1.5

	}
	windowDef BorderGlow {
		rect	0 ,0 ,-640 ,480 
		background	"gui/outerglowbar"
		matcolor	1 ,0.2 ,0.1 ,0.4 
		visible	1
	}
	windowDef Dirt {
	//edge dirt, surface smears
		rect	0 ,0 ,-640 ,480 
		matcolor	1, 1, 1 ,0.6
		visible	1
		background	"guis/assets/common/dirt1"
		noevents	1
	}

	windowDef fadeglass {
	//reflection
		rect	0 ,0 ,-640 ,480 
		visible	1
		background	"gui/fadeglass"
		matcolor	1 ,1 ,1 ,0.40 
	}


	windowDef Anim {
		notime	1


		onTime 0 {
			set "Button::visible" "0" ;
			transition "BorderGlow::matcolor" "1 0.2 0.1 0.4" "0.5 1 0.7 0.4" "600" ;
			transition "Desktop::matcolor" "0.8 0.1 0.1 0.6" "0.2 1 0.6 0.5" "600" ;
			transition "Bglow::matcolor" "1 0 0 0.1" "0 1 0.5 0.1" "600" ;
			set "WarningText::visible" "0" ;
			set "Warning2::visible" "0" ;
			set "Line1::visible" "0" ;
			set "Line2::visible" "0" ;
			transition "TextArea2::forecolor" "1 0.8 0.4 0.8" "1 0.8 0.4 0" "200" ;
		}
		onTime 200 {
			transition "Safe::forecolor" "0.2 1 0.5 0" "0.2 1 0.5 0.6" "600" ;
			transition "Safe1::forecolor" "1 1 1 0" "1 1 1 0.6" "600" ;
			set "TextArea2::text" "#str_01187" ;
			transition "TextArea2::forecolor" "1 0.8 0.4 0" "1 0.9 0.6 0.8" "600" ;
		}
		onTime 600 {
			set "Line3::visible" "1" ;
		}
	}

	windowDef Button {
		rect		0 ,0 ,640 ,480 
		backcolor	1, 1, 1, 0
		visible		1
		notime		1

		onAction {
		//	set	"cmd" "activate" ;
			set "cmd" "play guisounds_error" ;
			resetTime "0" ;
		}

		onTime 0 {
			set "Button::rect" "0 0 0 0" ;
		}

		onTime 5000 {
			set "Button::rect" "0 0 640 480" ;
		}
	}

}